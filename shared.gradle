// For building the open-source release of TalkBack.

ext {
    talkbackApplicationId = "com.android.talkback"
}

android {
    compileSdk 35
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    defaultConfig {
        buildConfigField "String", "APPLICATION_ID", "\"" + talkbackApplicationId + "\""
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        minSdkVersion 26
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }
    flavorDimensions = ["target"]
    productFlavors {
        phone {
            dimension "target"
        }
        wear {
            dimension "target"
        }
    }
    buildFeatures { buildConfig true }
}

configurations.configureEach {
    resolutionStrategy {
        eachDependency {
            // https://paste.googleplex.com/6584345232932864?raw
            //
            // 'androidx.wear:wear:1.2.0-rc01'
            // 'androidx.wear:wear-remote-interactions:1.0.0'
            //
            // If the above actual dependencies are updated and these libraries
            // update the version of org.jetbrains.kotlin:kotlin-stdlib, we
            // should update useVersion("1.8.0") to the updated version. Or, we
            // can remove these lines once these libraries don't conflict with
            // each other anymore.
            if ((requested.group == "org.jetbrains.kotlin") && (requested.name.startsWith("kotlin-stdlib"))) {
                useVersion("2.1.0")
            }
        }
    }
}

dependencies {

    // Google common
    implementation 'com.google.guava:guava:32.0.1-jre'
    implementation 'com.google.android.material:material:1.12.0'

    // Support library
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.collection:collection:1.4.5'
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.fragment:fragment:1.8.5'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'

    // Nullable
    implementation 'org.checkerframework:checker-qual:3.33.0'

    // Auto-value
    implementation 'com.google.auto.value:auto-value-annotations:1.10.4'
    annotationProcessor 'com.google.auto.value:auto-value:1.8.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'androidx.wear:wear:1.3.0'
    implementation 'androidx.wear:wear-remote-interactions:1.1.0'

    implementation 'com.google.zxing:core:3.5.2'

    // UI understanding
    implementation 'joda-time:joda-time:2.9.9'

    // Cloud services
    implementation 'com.mcxiaoke.volley:library:1.0.19'
}
